<net.rptools.maptool.model.MacroButtonProperties>
  <macroUUID>60638481-abef-4f11-b5bf-b4d103d8b20c</macroUUID>
  <saveLocation>Token</saveLocation>
  <index>26</index>
  <colorKey>default</colorKey>
  <hotKey>None</hotKey>
  <command>[h: toon = arg (0)]

&lt;!-- Create a dummy "unarmed" armor just to make the next everything easier --&gt;
[h: unarmored = json.set ("", "equipped", "true",
	"definition", json.set ("", "armorClass", 10,
				"baseArmorName", "Unarmored",
				"filterType", "Armor",
				"name", "Unarmored",
				"stealthCheck", 1,
				"armorTypeId", 1))]
&lt;!-- And a dummy "noShield" --&gt;
[h: noShield = json.set ("", "equipped", "true",
	"definition", json.set ("", "armorClass", 0,
				"baseArmorName", "No Shield",
				"filterType", "Shield",
				"name", "No Shield",
				"stealthCheck", 1,
				"armorTypeId", 4))]

&lt;!-- Get all armors and shields from inventory --&gt;
[h: allArmors = dndb_getArmor (toon)]

&lt;!-- Determine equipped armor --&gt;
[h: armors = json.path.read (allArmors, "[*].[?(@.definition.armorTypeId != 4)]")]
&lt;!-- Select first equipped armor. log.warn if there are more --&gt;
[h: equippedArmors = json.path.read (armors, "[*].[?(@.equipped == true)]")]
[h: equippedArmorNum = json.length (equippedArmors)]
[h, if (equippedArmorNum &gt; 1): log.warn ("Too many armors equipped, selecting first found armor")]
[h, if (equippedArmorNum &gt; 0): equippedArmor = json.get(equippedArmors, 0); 
	equippedArmor = unarmored]
&lt;!-- adorn the armor with basic details --&gt;
[h: equippedArmor = dndb_setArmorBonus (equippedArmor)]
[h: equippedArmorName = json.path.read (equippedArmor, "definition.name")]

&lt;!-- adorn the armor with bonuses --&gt;
&lt;!-- New approach, get modifiers all at once --&gt;
[h: log.debug ("Selecting equipped armor: " + equippedArmor)]

&lt;!-- determine equipped shield --&gt;
[h: shields = json.path.read (allArmors, "[*].[?(@.definition.armorTypeId == 4)]")]
&lt;!-- Select first equipped shield. log.warn if there are more --&gt;
[h: equippedShields = json.path.read (shields, "[*].[?(@.equipped == true)]")]
[h: equippedShieldNum = json.length (equippedShields)]
[h, if (equippedShieldNum &gt; 1): log.warn ("Too many shields equipped, selecting first found shield")]
[h, if (equippedShieldNum &gt; 0): equippedShield = json.get(equippedShields, 0); 
	equippedShield = noShield]
[h: equippedShield = dndb_setArmorBonus (equippedShield)]
[h: log.debug ("Selecting equipped shield: " + equippedShield)]

&lt;!-- Calculate Dexterity bonus --&gt;
[h: attributes = dndb_getAbilities (toon)]
[h: dexBonus = json.get (attributes, "dexBonus")]

&lt;!-- Apply dex bonus --&gt;
&lt;!-- rules: --&gt;
&lt;!-- no or light armor: full dex bonus --&gt;
&lt;!-- medium armor: dex bonus, up to +2 --&gt;
&lt;!-- heavy armor: No dex bonus (positive or negative) --&gt;
[h: armorTypeId = json.path.read (equippedArmor, "definition.armorTypeId")]
[h: log.debug ("armorTypeId: " + armorTypeId)]
[h, switch ( armorTypeId ):
	case "1" : dexBonus = dexBonus;
	case "2" : dexBonus = math.min (dexBonus, 2);
	case "3" : dexBonus = 0
]
[h: dexBonus = round (dexBonus)]
[h: log.debug ("dexBonus (calculated): " + dexBonus)]

&lt;!-- Look for other modifiers --&gt;
[h: bonus = 0]
[h: searchObj = json.set ("", "object", toon, 
						"_searchType", "=~",
						"subType", "/.*armor-class/")]
[h: modifiers = dndb_searchGrantedModifiers (searchObj)]

[h: totalBonus = 0]
[h, foreach (modifier, modifiers), code: {
	[h: bonus = dndb_getModValue (toon, modifier)]
	&lt;!-- test some specific conditions --&gt;
	[h: id = json.get (modifier, "id")]
	[h: unarmoredRequired = 0]
	[h, switch (id):
		case "2206": unarmoredRequired = 1;
		default: totalBonus = totalBonus + bonus
	]
	[h, if (unarmoredRequired &gt; 0 &amp;&amp; equippedArmorName == "Unarmored"): totalBonus = totalBonus + bonus]
}]

&lt;!-- And the customizations, by TypeID --&gt;

&lt;!-- typeId 4 - Override base + DEX --&gt;
&lt;!-- do type 4 before type 1 --&gt;
[h: overrideValue = -1]
[h: customSearchObject = json.set ("", "object", json.path.read (toon, "data.characterValues"),
										"typeId", "4",
										"property", "value")]
[h: overridePlusDexValues = dndb_searchJsonObject (customSearchObject)]
[h, if (json.length (overridePlusDexValues) &gt; 0): overrideValue = json.get (overridePlusDexValues, 0)]

&lt;!-- Not recognizing a difference between types 1 and 4. So for now, they act the same --&gt;
&lt;!-- typeId 1 - Override --&gt;
[h: customSearchObject = json.set (customSearchObject, "typeId", "1")]
[h: overrideValues = dndb_searchJsonObject (customSearchObject)]
[h, if (json.length (overrideValues) &gt; 0): overrideValue = json.get (overrideValues, 0)]

&lt;!-- typeId 2 - Magic bonus --&gt;
[h: customSearchObject = json.set (customSearchObject, "typeId", "2")]
[h: magicBonusValues = dndb_searchJsonObject (customSearchObject)]
[h, if (json.length (magicBonusValues) &gt; 0): totalBonus = totalBonus + json.get (magicBonusValues, 0)]

&lt;!-- typeId 3 - Additional Misc bonus --&gt;
[h: customSearchObject = json.set (customSearchObject, "typeId", "3")]
[h: miscBonusValues = dndb_searchJsonObject (customSearchObject)]
[h, if (json.length (miscBonusValues) &gt; 0): totalBonus = totalBonus + json.get (miscBonusValues, 0)]


&lt;!-- And build it --&gt;
&lt;!-- Lets recap the players --&gt;
&lt;!-- total: every thing --&gt;
&lt;!-- dexterity: dex bonus, modified --&gt;
&lt;!-- armor: just the equipped armor --&gt;
&lt;!-- shield: just the equipped shield --&gt;
&lt;!-- feature: whatever the feature is providing --&gt;
[h: armorBonus = json.get (equippedArmor, "baseArmorClass")]
[h: log.debug ("armorBonus: " + armorBonus)]
[h: shieldBonus = json.get (equippedShield, "baseArmorClass")]
[h: log.debug ("shieldBonus: " + shieldBonus)]
&lt;!-- calculate what we have so far --&gt;
[h: totalACBonus = armorBonus + shieldBonus + dexBonus + totalBonus]
[h, if (overrideValue &gt; -1): totalACBonus = overrideValue]
&lt;!-- build the base object --&gt;
[h: acObj = json.set ("", "Dexterity", dexBonus,
		"Armor", armorBonus,
		"Shield", shieldBonus,
		"Bonus", totalBonus)]
&lt;!-- hope this added up correctly --&gt;
[h: acObj = json.set (acObj, "total", totalACBonus)]
[h: macro.return = acObj]</command>
  <label>dndb_getAC</label>
  <group>Character</group>
  <sortby/>
  <autoExecute>true</autoExecute>
  <includeLabel>false</includeLabel>
  <applyToTokens>false</applyToTokens>
  <fontColorKey>black</fontColorKey>
  <fontSize>1.00em</fontSize>
  <minWidth/>
  <maxWidth/>
  <allowPlayerEdits>false</allowPlayerEdits>
  <toolTip>Given a character, returns an object with all AC bonuses.

arg(0) : toon

returns acObj
(example)

{
	"total" : 18,
	"Armor" : 10,
	"Dexterity" : 1,
	"Unarmored Bonus" : 4,
	"Shield" : 3
}</toolTip>
  <displayHotKey>true</displayHotKey>
  <commonMacro>false</commonMacro>
  <compareGroup>true</compareGroup>
  <compareSortPrefix>true</compareSortPrefix>
  <compareCommand>true</compareCommand>
  <compareIncludeLabel>true</compareIncludeLabel>
  <compareAutoExecute>true</compareAutoExecute>
  <compareApplyToSelectedTokens>true</compareApplyToSelectedTokens>
</net.rptools.maptool.model.MacroButtonProperties>
