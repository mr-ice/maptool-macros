<net.rptools.maptool.model.MacroButtonProperties>
  <macroUUID>9d618e17-7188-468f-86b8-48185c9544d2</macroUUID>
  <saveLocation>Token</saveLocation>
  <index>15</index>
  <colorKey>default</colorKey>
  <hotKey>None</hotKey>
  <command>&lt;!-- Callers must pass in the character json themselves. No getter methods should shoulder the --&gt;
&lt;!-- responsibility of calling dndb_getCharJSON --&gt;
&lt;!-- Requires two parameters: toon json and weapon object --&gt;
[h: toon = arg(0)]
[h: weapon = arg (1)]

[h: log.debug ("dndb_getAttackModifierForWeapon")]

&lt;!-- Calculates attack bonus, including that which comes from the toon's attributes --&gt;
[h: attributes = dndb_getAbilities (toon)]
[h: strBonus = round (math.floor((json.get (attributes, "str") - 10 ) / 2))]
[h: dexBonus = round (math.floor((json.get (attributes, "dex") - 10 ) / 2))]
[h: finesseBonus = max (strBonus, dexBonus)]
[h: log.debug ("strBonus: " + strBonus + " - dexBonus: " + dexBonus + " - finesseBonus" + finesseBonus)]

[h, if (json.get (weapon, "attackType") == "Melee"): abilityBonus = strBonus; abilityBonus = dexBonus]
[h: finesse = json.path.read (weapon, "properties.[?(@.name == 'Finesse')]")]
[h, if (json.length (finesse) &gt; 0): abilityBonus = finesseBonus]
[h: totalAtkBonus = abilityBonus]
[h: log.debug ("abilityBonus: " + abilityBonus)]

&lt;!-- apply proficiency bonus --&gt;
[h: proficiencyBonus = dndb_getProficiencyBonus (toon)]
[h: proficient = json.get (weapon, "proficient")]
[h, if (proficient &gt; 0): totalAtkBonus = totalAtkBonus + proficiencyBonus]
[h: log.debug ("proficiencyBonus: " + proficiencyBonus + " - totalAtkBonus: " + totalAtkBonus)]

[h: classAtkModifiers = json.path.read (toon, "data.modifiers.class..[?(@.type == 'bonus')]")]

&lt;!-- WIP: we only know of some class abilities to parse --&gt;
[h, foreach (classAtkModifier, classAtkModifiers), code : {
	[h: bonus = json.get (classAtkModifier, "value")]
	[h: qualified = dndb_isWeaponModifierApplicable (classAtkModifier, weapon)]
	[h, if (qualified &gt; 0), code: {
		[h: totalAtkBonus = totalAtkBonus + bonus]
		[h: log.debug (json.indent (classAtkModifier, 3))]
		[h: log.debug ("qualified: " + qualified)]

	}]
}]

[h: log.debug ("totalAtkBonus after class: " + totalAtkBonus)]
&lt;!-- no Race bonus to apply, yet. need a use case --&gt;


&lt;!-- apply item bonuses only if equipped --&gt;
[h: itemAtkModifiers = json.path.read (toon, "data.modifiers.item..[?(@.type == 'bonus')]")]
&lt;!-- for ech itemDamageMod, get the componentId. Find the item in inventory with the matching id and check equipped --&gt;
[h, foreach (itemAtkModifier, itemAtkModifiers), code: {
	[h: qualified = dndb_isWeaponModifierApplicable (itemAtkModifier, weapon)]
	[h, if (qualified &gt; 0): log.debug (json.indent (itemAtkModifier, 3))]
	[h: log.debug ("qualified: " + qualified)]
	
	[h: componentId = json.get (itemAtkModifier, "componentId")]
	[h: items = json.path.read (toon, "data.inventory..[?(@.definition.id == '" + componentId + "')]")]
	&lt;!-- should only be one --&gt;
	[h: item = json.get (items, 0)]
	[h: bonus = json.get (itemAtkModifier, "value")]
	[h: equipped = json.get (item, "equipped")]
	[h, if (equipped != "true"): qualified = 0]
	[h: log.debug ("Qualified after equipped: " + qualified)]
	[h, if (qualified &gt; 0): totalAtkBonus = totalAtkBonus + bonus]
}]

[h: log.debug ("totalAtkBonus after item: " + totalAtkBonus)]

&lt;!-- finally, bonuses on the weapon itself. No check for equipped here. --&gt;
[h: bonus = json.get (weapon, "bonus")]
[h: totalAtkBonus = totalAtkBonus + bonus]
[h: log.debug ("totalAtkBonus after weapon bonus: " + totalAtkBonus)]

[h: macro.return = totalAtkBonus]</command>
  <label>dndb_getAttackModifierForWeapon</label>
  <group>Utility</group>
  <sortby/>
  <autoExecute>true</autoExecute>
  <includeLabel>false</includeLabel>
  <applyToTokens>false</applyToTokens>
  <fontColorKey>black</fontColorKey>
  <fontSize>1.00em</fontSize>
  <minWidth/>
  <maxWidth/>
  <allowPlayerEdits>false</allowPlayerEdits>
  <toolTip/>
  <displayHotKey>true</displayHotKey>
  <commonMacro>false</commonMacro>
  <compareGroup>true</compareGroup>
  <compareSortPrefix>true</compareSortPrefix>
  <compareCommand>true</compareCommand>
  <compareIncludeLabel>true</compareIncludeLabel>
  <compareAutoExecute>true</compareAutoExecute>
  <compareApplyToSelectedTokens>true</compareApplyToSelectedTokens>
</net.rptools.maptool.model.MacroButtonProperties>
